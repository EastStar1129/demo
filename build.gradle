buildscript {
	repositories {
		mavenCentral()
	}
}

plugins {
	id 'java'
	id 'java-library'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
}

bootJar.enabled = false
jar.enabled = false

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'com.cms'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '21'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		// lombok
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		// meta-data
		annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

		// test
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

project(':database') {
	dependencies {
		api 'org.springframework.boot:spring-boot-starter-data-jpa'
		//	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
		runtimeOnly 'com.h2database:h2'
		runtimeOnly 'org.postgresql:postgresql'

		//dev
		developmentOnly 'org.springframework.boot:spring-boot-devtools'
	}

	bootJar { enabled = false }
	jar { enabled = false }
}

project(':admin') {
	dependencies {
		implementation project(':database')

		//web
		implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
		implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
		implementation 'org.springframework.boot:spring-boot-starter-web'

		//security
		implementation 'org.springframework.boot:spring-boot-starter-security'
	}
}

project(':client') {
	dependencies {
		implementation project(':database')

		//web
		implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
		implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
		implementation 'org.springframework.boot:spring-boot-starter-web'
	}
}